plugins {
    alias(libs.plugins.android.application)
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}


android {
    namespace 'tianci.dev.xptranslatetext'
    compileSdk 34

    defaultConfig {
        applicationId "tianci.dev.xptranslatetext"
        minSdk 29
        targetSdk 34
        versionCode 2000
        versionName "2.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(
                    keystoreProperties.getProperty('androidStoreFile', 'key.jks')
            )
            storePassword keystoreProperties.getProperty('androidStorePassword', '')
            keyAlias keystoreProperties.getProperty('androidKeyAlias', '')
            keyPassword keystoreProperties.getProperty('androidKeyPassword', '')
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def versionName = variant.versionName
            outputFileName = "XPTranslateText-${versionName}.apk"
        }
    }
}

dependencies {
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])

    implementation libs.appcompat
    implementation libs.material
    implementation libs.recyclerview
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core

    implementation 'androidx.preference:preference:1.2.1'
    implementation libs.mlkit.translate
    implementation libs.mlkit.language.id
}
